apply plugin: 'maven-publish'
apply plugin: 'fabric-loom'
apply plugin: 'com.replaymod.preprocess'

int mcVersion = 1

preprocess {
  mcVersion = vars.get()["MC"]
  tabIndentation = false
}

repositories {
  maven {
    url 'https://masa.dy.fi/maven'
  }
  maven {
    url 'https://jitpack.io'
  }
  maven {
    url 'https://www.cursemaven.com'
  }
  maven {
    url 'https://maven.terraformersmc.com/releases'
  }
}

dependencies {
  // loom
  minecraft "com.mojang:minecraft:${project.minecraft_version}"
  mappings loom.officialMojangMappings()
  modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
  // MixinExtras
  implementation "com.github.LlamaLad7:MixinExtras:0.1.1"
  annotationProcessor "com.github.LlamaLad7:MixinExtras:0.1.1"
  include "com.github.LlamaLad7:MixinExtras:0.1.1"
}

loom {
  runConfigs.all {
    // to make sure it generates all "Minecraft Client (:subproject_name)" applications
    ideConfigGenerated true
    runDir '../../run'
    vmArgs '-Dmixin.debug.export=true'
  }
}

// loom {
//   accessWidenerPath = file("src/main/resources/modid.accesswidener")
// }

remapJar {
  remapperIsolation = true
}

if (mcVersion >= 11800) {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}
else if (mcVersion >= 11700) {
  sourceCompatibility = JavaVersion.VERSION_16
  targetCompatibility = JavaVersion.VERSION_16
}
else {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

String versionSuffix = ''
if (System.getenv("BUILD_RELEASE") != "true") {
  String buildNumber = System.getenv("BUILD_ID")
  versionSuffix += buildNumber != null ? ('+build.' + buildNumber) : '-SHAPSHOT'
}
String fullModVersion = project.mod_version + versionSuffix

version = 'v' + fullModVersion
group = project.maven_group
archivesBaseName = project.archives_base_name + '-mc' + project.minecraft_version

processResources {
  inputs.property "version", fullModVersion

  filesMatching("fabric.mod.json") {
    def valueMap = [
        "id": project.mod_id,
        "name": project.mod_name,
        "version": fullModVersion,
        "minecraft_dependency": project.minecraft_dependency
    ]
    expand valueMap
  }
}

tasks.withType(JavaCompile) {
  options.encoding = "UTF-8"
  // options.compilerArgs << "-Xlint:deprecation" << "-Xlint:removal" << "-Xlint:unchecked"
  // options.warnings = true
}

java {
  // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
  // if it is present.
  // If you remove this line, sources will not be generated.
  withSourcesJar()
}

jar {
  from(rootProject.file('LICENSE')) {
    rename { "${it}_${project.archivesBaseName}"}
  }
}

// configure the maven publication
publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId archivesBaseName
      from components.java
    }
  }

  // select the repositories you want to publish to
  repositories {
    // uncomment to publish to the local maven
    // mavenLocal()
  }
}
